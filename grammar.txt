# {..} : 0 or more
# (..)*  : 0 or more
# [..] : optional

Module = ['module'] Import* TopDecl*

Import = 'import' STRING
TopDecl = [Visibility] Decl
Decl = ValDecl | FuncDecl | StructDecl
ValDecl = ('val' | 'var') IDENT ':' Type ['=' Expr] ';'
FuncDecl = 'func' IDENT Func
StructDecl = 'struct' IDENT Struct

Visibility = 'ext' | 'int' | 'rst'
Type = IDENT ['.' IDENT] 
StructField = [Visibility] ['static' | 'val' | 'var'] IDENT ':' Type ['=' Expr]
Struct= '{' [StructField {',' StructField} [',']]'}'
FuncField = ['val' | 'var'] IDENT ':' Type
FuncSignature = '(' [FuncField {',' FuncField} [',']] ')' ['->' Type]
Func = FuncSignature Block

Block = '{' Stmt* '}'
Stmt = Block | ValDeclStmt | PrintStmt | ExprStmt | AssignStmt | 'if' IfStmt | WhileStmt 
        | ReturnStmt | 'break' ';' | 'continue' ';'
ValDeclStmt = ValDecl ';'
PrintStmt = 'print' Expr ';'
AssignStmt = Expr ('=' | '+=' | '-=' | '*=' | '/=' | '%=' ) Expr ';'
ExprStmt = Expr ';'
IfStmt = Expr BlockStmt [('elif' IfStmt) | ('else' Block)]
WhileStmt = 'while' '{' Stmt* '}'
ReturnStmt = 'return' [Expr] ';'

Expr = LogicalOr 
LogicalOr = LogicalAnd {'||' LogicalAnd}
LogicalAnd = Equality {'&&' Equality}
Equality = Comparison {( '!=' | '==' ) Comparison}
Comparison = Term {( '>' | '>=' | '<' | '<=' ) Term}
Term = Factor {( '-' | '+' ) Factor}
Factor = Unary {( '/' | '%' | '*' ) Unary}
Unary = (( '!' | '-' ) Expr) | Operand
Operand = Primary | BasicLiteral | StructLiteral | '(' Expr ')'

Primary = IDENT | DotIdent | FuncCall 
FuncCall = Primary '(' [Expr {',' Expr} ] ')'
DotIdent = Primary '.' IDENT

BasicLiteral = INTEGER | FLOAT | STRING | 'false' | 'true'
KeyValue = IDENT '=' Expr
StructLiteral = Type '{' [KeyValue {',' KeyValue} [',]] '}'
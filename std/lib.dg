include "libc.dg"

fun println(str: &[i8]) {
    print(str)
    putchar('\n')
}

fun printiln(i: i64) {
    printi(i)
    putchar('\n')
}

fun printbln(b: bool) {
    if b {
        println("true")
    } else {
        println("false")
    }
}

fun print(str: &[i8]) {
    for i: usize = 0; i < len(str); i++ {
        putchar(str[i])
    }
}

fun printi(i: i64) {
    if i < 0 {
        putchar('-')
        printu(-i as u64)
    } else {
        printu(i as u64)
    }
}

fun printu(i: u64) {
    if i >= 10 {
        printu(i/10)
    }
    putchar(('0' + i%10) as i32)
}

fun readln(buf: &var [i8], null_terminate: bool): &var [i8] {
    var max = len(buf)
    if null_terminate and max > 0 {
        max--
    }
    var end = false
    var n: usize
    while n < max and not end {
        val ch = getchar()
        if ch == '\r' {
            // Wait for '\n'
        } elif ch == '\n' {
            end = true
        } else {
            buf[n] = ch as i8
            n++
        }
    }
    if n == max and not end {
        return null
    }
    if null_terminate {
        buf[n] = 0
    }
    return &var buf[:n]
}
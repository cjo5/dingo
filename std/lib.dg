pub module libc {
    include "libc.dg"
}

pub module math {
    pub use c = ::libc
    pub use c::sqrt
    pub use c::pow
    pub use c::tan
    pub use c::cos
    pub use c::sin

    pub val e: F32  = 2.71828182845904523536028747135266249775724709369995
    pub val pi: F32 = 3.14159265358979323846264338327950288419716939937510

    pub val minI8: I8   = -128
    pub val maxI8: I8   = 127
    pub val minI16: I16 = -32_768
    pub val maxI16: I16 = 32_767
    pub val minI32: I32 = -2_147_483_648
    pub val maxI32: I32 = 2_147_483_647
    pub val minI64: I64 = -9_223_372_036_854_775_808
    pub val maxI64: I64 = 9_223_372_036_854_775_807

    pub val maxU8: U8   = 255
    pub val maxU16: U16 = 65_535
    pub val maxU32: U32 = 4_294_967_295
    pub val maxU64: U64 = 18_446_744_073_709_551_615

    pub val maxF32: F32 = 3.40282346638528859811704183484516925440e+38
    pub val maxF64: F64 = 1.797693134862315708145274237317043567981e+308
}

pub module io {
    pub use c = ::libc
    pub use c::putchar

    pub fun println(str: &[Byte]) {
        print(str)
        putchar('\n')
    }

    pub fun print(str: &[Byte]) {
        for i: USize = 0; i < len(str); i++ {
            putchar(str[i])
        }
    }

    pub fun printbln(b: Bool) {
        if b {
            println("true")
        } else {
            println("false")
        }
    }

    pub fun printiln(i: I64) {
        printi(i)
        putchar('\n')
    }

    pub fun printi(i: I64) {
        if i < 0 {
            putchar('-')
            printu(-i as U64)
        } else {
            printu(i as U64)
        }
    }

    pub fun printuln(i: U64) {
        printu(i)
        putchar('\n')
    }

    pub fun printu(i: U64) {
        if i >= 10 {
            printu(i/10)
        }
        putchar(('0' + i%10) as I32)
    }

    pub fun printft(f: F64) {
        c::printf(c"%g", f)
    }

    pub fun printftln(f: F64) {
        c::printf(c"%g\n", f)
    }

    pub fun readln(buf: &var [Byte], null_terminate: Bool) &var [Byte] {
        var max = len(buf)
        if null_terminate and max > 0 {
            max--
        }
        var end = false
        var n: USize
        while n < max and not end {
            val ch = c::getchar()
            if ch == '\r' {
                // Wait for '\n'
            } elif ch == '\n' {
                end = true
            } else {
                buf[n] = ch as Byte
                n++
            }
        }
        if n == max and not end {
            return null
        }
        if null_terminate {
            buf[n] = 0
        }
        return &var buf[:n]
    }
}

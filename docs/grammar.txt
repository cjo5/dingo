# [..] : 0 or 1
# {..} : 0 or more
# a? : 0 or 1 a
# a* : 0 or more a
# a+ : 1 or more a
# a | b : a or b

ModuleBody = (Module | Include | TopLevelDecl)*
Module = 'module' Name '{' ModuleBody '}'
Include = 'Include' STRING EOS
ScopeName =  ('mself' | 'msuper' {'.' 'msuper'}) ['.' Name] | '.'? Name
Name = IDENT ['.' Name]
EOS = ';' | EOF

TopLevelDecl = [Visibility? (ExternDecl | ImportDecl | StructDecl | FuncDecl | Decl)] EOS
Visibility = 'pub' | 'priv' 
Extern = 'extern' ['(' IDENT ')']
ExternDecl = Extern (ValDecl | FuncDecl)
StructDecl = 'struct' IDENT StructBody?
FuncDecl = 'fun' IDENT FuncSignature Block?
Decl = TypeDecl | ValDecl | ImportDecl
TypeDecl = 'typealias' IDENT '=' Type
ValDecl = ('val' | 'var') IDENT (Type | (Type? '=' Expr))

ImportDecl = ('import' | 'importlocal') ImportName [':' ('('ImportList')' | ImportList) ]
ImportName = [('_' | IDENT) '='] ScopeName
ImportList = ImportItem {',' ImportItem} ','?
ImportItem = [('_' | IDENT) '='] IDENT

Field = (['val' | 'var'] (IDENT | '_'))? Type
StructBody= '{' {Field ';'} '}'
FuncSignature = '(' [Field {',' Field} ','?] ')' Type?

Type = NestedType | PointerType | ArrayType | FuncType | ScopeName
NestedType = '(' Type ')'
PointerType = '&' ['val' | 'var'] Type
ArrayType = '[' Type [INTEGER ':'] ']'
FuncType = Extern? 'fun' ['[' IDENT ']'] FuncSignature

Block = '{' Stmt* '}'
Stmt = [Block | Decl | AssignOrExprStmt | 'if' IfStmt | WhileStmt |
        ForStmt | ReturnStmt | DeferStmt | BranchStmt ] EOS
ExprOrAssignStmt = Expr ['++' | '--' | (('=' | '+=' | '-=' | '*=' | '/=' | '%=' ) Expr)]
IfStmt = Condition Block [('elif' IfStmt) | ('else' Block)]
WhileStmt = 'while' Condition Block
ForStmt 'for' [IDENT Type? '=' Expr] ';' Condition? ';' ExprOrAssignStmt? Block
ReturnStmt = 'return' [Expr]
BranchStmt = 'break' | 'continue'
DeferStmt = 'defer' ExprOrAssignStmt

Condition = UnaryOp? Operand Primary AsExpr [BinaryOp Condition]
Expr = UnaryOp? (Operand | StructLit) Primary AsExpr [BinaryOp Expr]
BinaryOp = '||' | '&&' | '!=' | '==' | '>' | '>=' | '<' | '<='
                | '-' | '+' | '/' | '%' | '*'
UnaryOp = ('!' | '-' | '*') | ('&' ['val' | 'var']) 
AsExpr = ['as' Type]
Operand = NestedExpr | LenExpr | SizeExpr | ScopeName | BasicLit | ArrayLit | FuncLit
NestedExpr = '(' Expr ')'
LenExpr = 'len' '(' Expr ')'
SizeExpr = 'sizeof' '(' Type ')'

ArgExpr = [IDENT ':'] Expr
ArgumentList = [ArgExpr {',' ArgExpr} ','?]

Primary = [SliceExpr | IndexExpr | FuncCall | DotExpr]
SliceExpr = '[' Expr? ':' Expr? ']'
IndexExpr = '[' Expr ']' Primary
FuncCall = '(' ArgumentList )' Primary
DotExpr = '.' IDENT Primary

BasicLit = Number | CHAR | (Name? STRING) | 'true' | 'false' | 'null'
Number = (INTEGER | FLOAT) Name?
StructLit = ScopeName '{' ArgumentList '}'
ArrayLit  = ArrayType '{' [Expr {',' Expr} ','?] '}'
FuncLit = Extern? 'fun' FuncSignature Block

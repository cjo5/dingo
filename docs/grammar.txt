# {..} : 0 or more
# [..] : 0 or 1
# a* : 0 or more a
# a? : 0 or 1 a
# a | b : a or b

File = ['module' Name ';'] Include* TopLevel*
Name = IDENT ['.' Name]
EOS = ';' | EOF

Include = 'Include' STRING EOS
TopLevel = Visibility? (Import | ExternDecl | StructDecl | FuncDecl | Decl) EOS
Visibility = 'pub' | 'priv' 
Extern = 'extern' ['(' IDENT ')']
Import = 'import' Name ['as' IDENT]
ExternDecl = Extern (ValDecl | FuncDecl)
StructDecl = 'struct' IDENT StructBody?
FuncDecl = 'fun' IDENT FuncSignature Block?
Decl = TypeDecl | ConstDecl | ValDecl
TypeDecl = 'alias' IDENT Type
ConstDecl = 'const' IDENT (Type | (Type? '=' Expr))
ValDecl = ('val' | 'var') IDENT (Type | (Type? '=' Expr))

Field = (['val' | 'var'] (IDENT | '_'))? Type
StructBody= '{' {Field ';'} '}'
FuncSignature = '(' [Field {',' Field} ','?] ')' Type?

Type = NestedType | PointerType | ArrayType | FuncType | Name
NestedType = '(' Type ')'
PointerType = '&' ['val' | 'var'] Type
ArrayType = '[' Type [INTEGER ':'] ']'
FuncType = Extern? 'fun' ['[' IDENT ']'] FuncSignature

Block = '{' Stmt* '}'
Stmt = [Block | Decl | AssignOrExprStmt | 'if' IfStmt | WhileStmt |
        ForStmt | ReturnStmt | 'break' | 'continue'] EOS
ExprOrAssignStmt = Expr ['++' | '--' | (('=' | '+=' | '-=' | '*=' | '/=' | '%=' ) Expr)]
IfStmt = Condition Block [('elif' IfStmt) | ('else' Block)]
WhileStmt = 'while' Condition Block
ForStmt 'for' [IDENT Type? '=' Expr] ';' Condition? ';' ExprOrAssignStmt? Block
ReturnStmt = 'return' [Expr]

Condition = UnaryOp? Operand Primary AsExpr [BinaryOp Condition]
Expr = UnaryOp? (Operand | StructLit) Primary AsExpr [BinaryOp Expr]
BinaryOp = '||' | '&&' | '!=' | '==' | '>' | '>=' | '<' | '<='
                | '-' | '+' | '/' | '%' | '*'
UnaryOp = ('!' | '-' | '*') | ('&' ['val' | 'var']) 
AsExpr = ['as' Type]
Operand = NestedExpr | LenExpr | SizeExpr | Name | BasicLit | ArrayLit | FuncLit
NestedExpr = '(' Expr ')'
LenExpr = 'len' '(' Expr ')'
SizeExpr = 'sizeof' '(' Type ')'

ArgExpr = [IDENT ':'] Expr
ArgumentList = [ArgExpr {',' ArgExpr} ','?]

Primary = [SliceExpr | IndexExpr | FuncCall | DotExpr]
SliceExpr = '[' Expr? ':' Expr? ']'
IndexExpr = '[' Expr ']' Primary
FuncCall = '(' ArgumentList )' Primary
DotExpr = '.' IDENT Primary

BasicLit = Number | CHAR | (Name? STRING) | 'true' | 'false' | 'null'
Number = (INTEGER | FLOAT) Name?
StructLit = Name '{' ArgumentList '}'
ArrayLit  = '[' [Expr {',' Expr} ','?] ']'
FuncLit = Extern? 'fun' FuncSignature Block

# The Dingo Language

## Index
- [Files and Modules](#files-and-modules)
- [Comments](#comments)
- [Semicolons](#semicolons)
- [Variables](#variables)
- [Functions](#functions)
- [References / Pointers](#references--pointers)
- [Arrays](#arrays)
- [Slices](#slices)
- [Structs](#structs)
- [Typealias](#typealias)
- [Type Casting](#type-casting)
- [If](#if)
- [For / While](#for--while)
- [Defer](#defer)
- [Sizeof](#sizeof)
- [Memory Management](#memory-management)
- [C](#c)
- [Strings](#strings)
- [Booleans](#booleans)
- [Numbers](#numbers)
- [Basic Operators](#other-operators)
- [Assignments](#assignments)
- [Grammar](grammar.md)
- [Operator Precedence](#operator-precedence)
- [Keywords](#keywords)

## Files and Modules
A module is a namespace for grouping related code. Modules can be nested, in which case they share a common prefix.
An identifier's Fully Qualified Name (FQN) is the path from the root module to the identifier.
Modules and top-level identifiers are either public ```pub``` or private ```priv``` (default).
The visibility of parent modules do not affect the visibility of child modules.

```rust
module foo {            // FQN: foo
    var bar i32         // FQN: foo.bar
    pub module baz {    // FQN: foo.baz
        pub var qux i32 // FQN: foo.baz.qux
    }
} 
```

A file can be included inside another file and module. This is equivalent to replacing the include statement with the included file's content.
The FQN of every identifier in the included file is prefixed with the FQN of the module where it's included.
The include path is either absolute or relative to the file with the include statement.

a.dg:
```rust
var bar i32
module baz {
    var qux i32    
}

```
b.dg:
```rust
module foo {
    include "a.dg"
    // The identifiers in a.dg are brought in to the foo module.
    // FQN: foo.bar
    // FQN: foo.baz
    // FQN: foo.baz.qux
}
```

A compilation unit (cunit) is a root file (passed as argument to dgc) plus all its included files.
Files are included in a breadth-first search manner and are loaded automatically during the parsing phase.
Compilation units have a one-to-one correspondence to object files generated by the compiler.

Multiple modules can be defined in the same cunit, as long as each module has a unique FQN within that cunit.
A module can access any other module/identifier defined in the same cunit.
Name lookups inside a module are relative by default. Absolute name lookups are prefixed with '.'.
Parent modules are not automatically searched in relative lookups. To lookup a name in a parent module, prefix the name with 'msuper'.
Name lookup is similar to how file system navigation on the command line works, where modules are directories and the final non-module identifier is the file.

```rust
fun hello() {}

module foo {
    fun bar() {
        hello()    // error
        .hello()   // ok
        baz.quux() // ok
    }
    module baz {
        fun qux() {
            bar()                 // error
            msuper.bar()          // ok
            msuper.msuper.hello() // ok
        }
        fun quux() {}
    }
}
```

A compilation context is a set of cunits. Modules defined in different cunits must be imported before they can be accessed.
A module can only be imported if it's declared as public. Every public module must have a unique FQN in a compilation context.
It's allowed to have modules in different cunits with the same FQN if at most one of them is public.
There are two types of import keywords: 'import' and 'importlocal'. 'import' searches for public modules in the same compilation context and 'importlocal' searches for local modules defined in the same cunit (public or private). 

a.dg:
```rust
module foo {
    importlocal bar: h1 = hello // ok
    import baz: h2 = hello      // ok
    import qux                  // error, not public

    fun hello{}
}

module bar {
    fun hello{}
}
```

b.dg:
```rust
pub module baz {
    pub fun hello{}
}

module qux {
    pub fun hello{}
}
```

**Summary**
- A compilation unit (cunit) is root file + includes.
- Files can be included inside any module.
- Modules in the same cunit can be accessed directly.
- Modules in different cunits must be imported before they can be accessed.
- A module must be unique in a cunit and a public module must be unique in a compilation context.
- There are three types of module scopes: self, parent and root.
    The self scope is used by default, '.' prefix uses root scope, and 'msuper' prefix uses parent scope.
- import looks up public modules in the same compilation context.
- importlocal looks up local modules in the same compilation unit.

## Comments
```rust
// Single line comment.

/*
    Mutiline comment.
    Another comment.
    /*
        Nested comment.
    */
*/
```

## Semicolons
Semicolons work in a similar way as in Go. That is, the grammar and parser assume that statements are terminated with semicolons; however, the lexer automatically inserts a semicolon in the token stream at the end of a line if it sees a token that can terminate a statement. See [here](https://github.com/cjo5/dingo/blob/d9bde812c35417635c3d221e262d2ec4b61acfe2/internal/frontend/lex.go#L169) for the exact tokens that the lexer checks for.

## Variables
```rust
var a: i32
var b: i32 = 1
var c = 2
val d: i32
val e: i32 = 1
val f = 2
```

```var``` defines a mutable variable and ```val``` defines an immutable value. The type can be omitted when there is an initializer. The variable/value is assigned a default value if there is no initializer.

## Functions
```rust
fun say_hello() {
    println("hello")
}

fun add(a: i32, b: i32): i32 {
    return a + b
}

fun inc(var a: i32): i32 {
    a += 1
    return a
}

say_hello()
add(a: 5, b: 10)
```

No return type means the function has no return value. Function calls support named arguments in arbitrary order. There can be no positional arguments after a named argument.
Function parameters are immutable by default, but can be made mutable by preceeding the name with 'var'.

Functions can be used as values and also defined inline (function literals). Though note that function literals are not closures and do not have access to variables in the enclosing scope.
```rust
val add_val: fun (_ i32, _ i32): i32 = add
val sub = fun(a: i32, b: i32) i32 {
    return a - b
}
```

## References / Pointers
```rust
var a: i32 = 5
val b: &i32 = &a         // immutable reference
val c: &var i32 = &var a // mutable reference
val d: &i32 = null       // default value

*b = 5 // error, b is an immutable reference
*c = 9 // ok

val e = 6
val f = &var e      // error, cannot take a mutable reference to an immutable value
```

## Arrays
```rust
var a: [i32:5] = [i32]{1, 2, 3, 4, 5} // allocated on the stack
len(a) // length of array
```

## Slices
```rust
var a: = [i32]{1, 2, 3, 4, 5}
val b: &[i32] = &a[1:3]          // immmutable slice 
val c: &var [i32] = &var a[:3]   // mutable slice 
val d: &[i32] = null             // default value

len(c) // length of slice
```

## Structs
```rust
struct Foo {
    a: i32       // immutable
    var b: i32   // mutable
}

val f = Foo(a: 5, b: 9) // allocated on the stack
```

## Typealias
```rust
typealias id = i32
```

Create an alias for a type. The two types are equivalent, and either type can be substituted for the other.

## Type Casting
```rust
val a: i64 = 5
val b = a as i32
```

## If
```rust
val a = 5
if a > 5 {
    println("Big")
} elif a < 5 {
    println("Small")
} else {
    println("OK")
}
```

Braces required.

## For / While
```rust
for i = 0; i < 5; i++ { // i immutable
    printiln(i)
}

for var i = 0; i < 5; i++ { // i mutable
    printiln(i)
    i++
}

var i = 0
while true {
    i++
    if (i % 2) == 0 {
        continue
    } elif i == 5 {
        break
    }
    printiln(i)
}
```

Braces required. ```continue``` and ```break``` as expected. 

## Defer
```rust
{
    defer println("Bye")
    defer printiln(3)
    defer printiln(2)
    defer printiln(1)
    println("Hello")
}
// output:
// Hello
// 1
// 2
// 3
// Bye
```

Defer execution of a statement until the end of the block. If a defer is executed, the deferred statement is guaranteed to be executed at the end of the enclosing scope, regardless of the control flow. The deferred statements are executed in reverse order of the defers.

## Sizeof
```rust
sizeof(i8)      // 1
sizeof(i64)     // 8
sizeof([i32:5]) // 4*5 = 20
```

Get the size of a type in bytes.

## Memory Management
Dynamic memory management is currently handled through the C API.

## C
Features to interface with the C ABI and runtime.

**Types**
```
c_void
c_char
c_uchar
c_short
c_ushort
c_int
c_uint
c_longlong
c_ulonglong
c_usize
c_float
c_double
```

References are currently used for C pointers, though no pointer arithmetic is allowed. 

**Functions** 
```rust
// Functions defined in C can be called from Dingo
extern fun free(ptr: &c_void)
extern fun malloc(size: c_usize): &var c_void

// Functions defined in Dingo can be called from C
extern fun do_stuff() {
    //...
}
```

Using ```extern``` on functions will enable C ABI and disable name mangling. 

**main**
```rust
extern fun main(argc: c_int, argv: &&c_char): c_int {
    return 0
}
```

Main function in Dingo.

## Strings
There are two types of string literals: normal and C-like. Normal string literals are immutable slices, and C-like strings are immutable references. Both string types are null-terminated.
```rust
val a: &[i8] = "Hello"
val b: &i8 = c"Bye
```

## Booleans
```rust
val a: bool = true
val b = false
```

## Numbers
**Types**
```rust
// signed integers
i8
i16
i32
i64
// unsigned integers
u8
u16
u32
u64
usize
// floating point
f32 
f64 
```

**Literal Samples**
```rust
15      // plain integer literal
100_000 // underscores to make large numbers more readable
100u8   // literals can have any numeric type as a suffix
0xFF    // hex
077     // octal
3.14    // plain floating point literal
1.234e2 // scientific notation: 1.234*10^2
1.234E2
```
## Basic Operators
**Binary Operators**
```rust
a + b   // addition
a - b   // subtraction
a * b   // mutliplication
a / b   // division
a % b   // remainder

a >= b  // greater than or equal to
a <= b  // less than or equal to
a == b  // equality
a != b  // inequality

// lazily evaluted
a and b // logical and
a or b  // logical or
```

**Unary Operators**
```rust
-a  // numerical negation
not a  // logical negation
```

## Assignments
```rust
a = 2   // normal assignment
a += 2  // a = a + 2
a -= 2  // a = a - 2
a *= 2  // a = a * 2
a /= 2  // a = a / 2
a %= 2  // a = a % 2
a++     // a = a + 1
a--     // a = a - 1
```

All assignments are statements.

## Grammar
See [grammar](grammar.md).

## Operator Precedence
```
Precedence  Associativity   Operation
1           None            (exp) len(a) sizeof(a) literal identifier
2           Left-to-right   a() a[i] a[i:j] a.b 
3           None            not -a &a *a 
4           None            a as b           
5           Left-to-right   a*b a/b a%b
6                           a+b a-b 
7                           < <= > >=
8                           == !=
9                           and
10                          or
```

## Keywords
```
and
as
break
continue
defer
elif
else
extern
false
for
fun
if
import
importlocal
include
len
module
mself
msuper
not
null
or
priv
pub
return
sizeof
struct
true
typealias
val
var
while
```

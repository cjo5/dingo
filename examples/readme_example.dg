// C function declarations
fun[c] abs(c_int) c_int
fun[c] putchar(c_int) c_int
fun[c] puts(&c_char) c_int

/*
    Comment
    /*
        Nested comment
    */
*/
fun[c] main() c_int {
    // Infer type [i32:5]
    var arr = [31, 4, -10, 9, 2]

    sort(&var arr[:])
    puts(c"Sorted ints:")
    printSlice(&arr[:])

    return 0
}

fun swap(x &var i32, y &var i32) {
    val tmp = *x
    *x = *y
    *y = tmp
}

fun sort(slice &var [i32]) {
    for i = 0; i < len(slice)-1; i++ {
        for j = 0; j < len(slice)-1; j++ {
            if slice[j] > slice[j+1] {
                swap(&var slice[j], &var slice[j+1])
            }
        }
    }
}

fun printSlice(slice &[i32]) {
    for i = 0; i < len(slice); i++ {
        puti(slice[i])
        puts(c"")
    }
}

// Recursively print each digit
fun puti(i i32) {
    val i2 = abs(i)
    if i2 < 10 {
        if i < 0 {
            putchar('-')
        }
    } else {
        puti(i/10)
    }
    putchar('0' + i2%10)
}
